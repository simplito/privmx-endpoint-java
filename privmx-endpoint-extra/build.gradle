plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

//Apply script for generating official documentation
if (file("build-documentation.gradle").exists()) {
    apply from: "build-documentation.gradle"
}

//Apply script for publishing maven dependency
if (file("build-publish-maven.gradle").exists()) {
    apply from: "build-publish-maven.gradle"
}

version = '2.3.0-rc2'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    testImplementation libs.junit
    api project(':privmx-endpoint')
}

javadoc {
    options.tags += ["category:xt:\"Category group\""]
}

//Removing tag @group from sources
sourcesJar {
    archiveVersion.set(project.version)
    filter(new Transformer<String, String>() {
        @Override
        String transform(String s) {
            if (
                    s.trim().matches("\\* *@group.*") ||
                            s.trim().matches("\\* *@folder.*") ||
                            s.trim().matches("\\* *@category.*")
            ) {
                return null
            }
            return s
        }
    })
}